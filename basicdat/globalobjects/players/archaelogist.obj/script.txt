//-----------------------------------------------------------------------------
// Snake phobia =)
IfStateIs0
  tmpargument = [SNAK]
  tmpdistance = BLAHENEMIES+BLAHFRIENDS   // Even friendly ones!
  SetTargetToWideBlahID
    tmpargument = 12
    PlayFullSound
    tmpargument = 1
    SetState
    SendMessage
    SetTargetToSelf
    tmpargument = 5
    DazeTarget
    tmpargument = 1500        // 1,5 minutes
    SetTime

    // Draw the text
    tmpdistance = 3         // Duration
    tmpargument = 8         // Message 8
    tmpturn = WHITE         // Message color
    DrawBillboard

IfTimeOut
  tmpargument = 0
  SetState

//-----------------------------------------------------------------------------
// Auto jump over corpses
IfBumped
  SetTargetToWhoeverBumped
  IfTargetIsAlive
    DoNothing
  Else
    tmpargument = LATCHJUMP
    PressLatchButton

//-----------------------------------------------------------------------------
// Gong sound
IfCleanedUp
  tmpargument = 2
  PlayFullSound

//------------------------------------------------------------------------------
// Yell at player
IfTooMuchBaggage
  tmpargument = 7
  SendMessageNear
  tmpargument = 10
  PlaySound
  tmpargument = 50        // Reuse speed
  SetReloadTime

//------------------------------------------------------------------------------
// Scream and shout
IfUsed
  tmpargument = rand % 6 + 13
  PlaySound

//------------------------------------------------------------------------------
// Handle being bored by cheering or sneezing
IfBored
  tmpargument = ACTIONMC
  DoAction
    tmpargument = 6
    SendMessageNear
    tmpargument = 11
    PlaySound

//------------------------------------------------------------------------------
// Handle death by sending a message and other stuff
IfKilled
  tmpargument = 8
  PlaySound
  tmpargument = 3
  IfArmorIs
    tmpargument = 5
  Else
    tmpargument = 0
  IfTargetIsOnSameTeam
    tmpargument = 3
    IfTargetIsSelf
      tmpargument = 4
  SendMessage

  // Drop goodies
  tmpargument = selflevel*selflevel*100
  DropMoney
  DropKeys

  // Make the character body
  tmpargument = 45
  SetBumpHeight

//------------------------------------------------------------------------------
// For helper AIs
IfLeaderKilled
  BecomeLeader

//------------------------------------------------------------------------------
// Handle being attacked by blocking or countering or running away
IfAttacked
  SetTargetToWhoeverAttacked
  IfTargetIsOnSameTeam
    tmpargument = 9       // "Stupid!"
    PlaySound
    tmpargument = 2
    SendMessageNear
  Else
    tmpargument = rand % 5 + 3  // Ouch!
    PlaySound

//------------------------------------------------------------------------------
End
//------------------------------------------------------------------------------
