// This function makes an item fall to the floor when spawned
IfSpawned
  tmpargument = ACTIONJB
  DoAction
  KeepAction
  MakeCrushValid

IfCrushed
  tmpx = selfx
  tmpy = selfy
  tmpdistance = selfz
  tmpargument = 15
  SpawnExactParticle
  SpawnExactParticle
  SpawnExactParticle
  SpawnExactParticle
  GoPoof
  tmpargument = 1
  SendMessageNear

// Make it lie on floor
IfDropped
  KeepAction
  tmpargument = 3
  PlaySound

// Tell them what they've won...
IfGrabbed
  SetTargetToWhoeverIsHolding
  IfTargetIsAPlayer
    tmpargument = 0
    SendMessageNear

// Make a sound
IfHitGround
  tmpargument = 1
  PlaySound

IfNotDropped
  SetTargetToWhoeverIsHolding
  tmpargument = 2
  SendMessageNear

IfTakenOut
  tmpargument = 2
  PlaySound
  SetTargetToWhoeverIsHolding
  IfTargetIsAPlayer
    tmpargument = 4
    SendMessageNear

// No longer needed, this is handled by the sourcecode now...
//IfNotPutAway
//  SetTargetToWhoeverIsHolding
//  tmpargument = 3
//  SendMessageNear

// Automatically attack closest lifeform
IfTimeOut

  tmpargument = rand & 15 + 20
  SetTime

  SetTargetToNearestLifeform
    tmpx = targetdistance
    tmpy = 150
    IfXIsLessThanY
      SetTargetToWhoeverIsHolding
        IfHeldInLeftHand
          tmpargument = ACTIONTA        // Thrust left
        Else
          tmpargument = ACTIONTC        // Thrust right
        TargetDoAction

// All done
End

