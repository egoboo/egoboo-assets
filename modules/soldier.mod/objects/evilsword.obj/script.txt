// Magical abillity
IfScoredAHit
  SetTargetToWhoeverIsHolding
    tmpargument = 256
    HealTarget

// No non-evil aligned sword users here...
IfGrabbed
  SetTargetToWhoeverIsHolding
  tmpargument = [EVIL]
  IfTargetHasSpecialID
    DoNothing
  Else
    DetachFromHolder
    tmpdistance = DAMAGEEVIL
    tmpargument = rand & 511 + 512  // 2-4 damage
    DamageTarget
    tmpargument = 3
    SendMessageNear
    tmpargument = 4
    PlaySound

// ZZ> This function makes an item fall to the floor when spawned
IfSpawned
  SetTargetToSelf
  tmpargument = ACTIONJB
  DoAction
  KeepAction
  MakeCrushValid

IfCrushed
  tmpx = selfx
  tmpy = selfy
  tmpdistance = selfz
  tmpargument = 1
  SpawnExactParticle
  SpawnExactParticle
  SpawnExactParticle
  SpawnExactParticle
  GoPoof
  tmpargument = 1
  SendMessageNear

// Make it lie on floor
IfDropped
  SetTargetToSelf
  KeepAction

// Make a sound
IfHitGround
  tmpargument = 1
  PlaySound

// Make a sound
IfTakenOut
  tmpargument = 2
  PlaySound
  SetTargetToWhoeverIsHolding
  IfTargetIsAPlayer
    tmpargument = 2
    SendMessageNear

// Tell them what they've won...
IfGrabbed
  SetTargetToWhoeverIsHolding
  IfTargetIsAPlayer
    tmpargument = 0
    SendMessageNear

// Magical glowing evil effect
tmpargument = 2
tmpx = 0
tmpy = 0
tmpdistance = rand % 9 // Pick a vertex along the blade ( look at model )
SpawnParticle

// All done
End

