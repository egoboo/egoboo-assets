//------------------------------------------------------------------------------
// ZZ> Most undead have an enchantment sustaining them
IfSpawned
  SetTargetToSelf
  SetOwnerToTarget
  EnchantTarget  // Enchant self...

  tmpargument = STATEGUARD
  SetState

//------------------------------------------------------------------------------
IfTargetKilled
  SetTargetToNearestEnemy
    tmpargument = STATECHARGE
    SetState
  Else
    tmpargument = STATEGUARD
    SetState

//------------------------------------------------------------------------------
// ZZ> Handle death by sending a message and other stuff
IfKilled
  tmpargument = 2
  PlaySound

  // Drop goodies
  tmpargument = selfmoney
  DropMoney
  DropKeys

  // Boss stuff...
  tmpx = selfmorale
  tmpy = 0
  IfXIsEqualToY
    tmpargument = 1
    OpenPassage
    ClearEndMessage
    tmpargument = 2
    AddEndMessage
    tmpargument = 3
    AddEndMessage
    tmpargument = 1
  Else
    tmpargument = 0
  SendMessageNear

  // Make the character body
  tmpargument = 0
  SetBumpHeight
  tmpargument = 0
  SetFlyHeight

//------------------------------------------------------------------------------
IfBumped
  SetTargetToWhoeverBumped
  IfTargetIsOnSameTeam
    tmpx = selfx
    tmpy = selfy
    tmpturn = targetturnto + 32768
    tmpdistance = 256
    Compass
    ClearWaypoints
    AddWaypoint
    SetTargetToOldTarget

//------------------------------------------------------------------------------
IfTimeOut

  // Spooky sounds
  tmpx = rand & 255
  tmpy = 5
  IfXIsLessThanY
    tmpargument = rand % 3 + 3
    PlaySound

  // Wait for the good guys to barge in...
  IfStateIsGuard
    tmpargument = 20
    SetTime

    tmpargument = passage
    SetTargetToSelf
    SetTargetToWhoeverIsInPassage
      DoNothing
    Else
      SetTargetToWideEnemy

    IfTargetIsOnHatedTeam
      tmpargument = rand & 15 + 50
      SetTime
      Run
      SetTurnModeToVelocity
      tmpx = targetx
      tmpy = targety
      tmpargument = STATECHARGE
      SetState
    Else
      Stop
      SetTurnModeToWatch
      tmpx = selfspawnx
      tmpy = selfspawny
      ClearWaypoints
      AddWaypoint

  // Chop the enemy
  IfStateIsCombat
    tmpargument = rand & 31 + 50
    SetTime
    tmpx = targetdistance
    tmpy = 180
    IfXIsLessThanY
      // Close enough to swing...
      IfHoldingMeleeWeapon
        PressLatchButton
        tmpargument = STATESURROUND
        SetState
    Else
      // Fall through to next...
      tmpargument = STATECHARGE
      SetState

  // Rush the enemy
  IfStateIsCharge
    IfTargetIsOnHatedTeam
      DoNothing
    Else
      SetTargetToNearestEnemy

    SetTurnModeToVelocity
    tmpx = targetdistance
    tmpy = 1400
    IfXIsMoreThanY
      // Find a new enemy
      SetTargetToNearestEnemy
        DoNothing
      Else
        tmpargument = STATEGUARD             // no enemies left
        SetState
      tmpargument = 5
      SetTime
    Else
      // Charge the enemy
      tmpx = targetx
      tmpy = targety
      tmpturn = targetturnto
      tmpdistance = 200
      Compass
      SetOldTarget
      SetTargetToNearestFriend                  // Dont flock
        tmpdistance = 500 - targetdistance > 1
        tmpturn = targetturnto + 32768
        Compass
        SetTargetToOldTarget
      ClearWaypoints
      AddWaypoint
      tmpargument = STATECOMBAT
      SetState
      tmpargument = rand & 31 + 50
      SetTime

  // Follow through
  IfStateIsSurround
    tmpargument = rand & 31 + 50
    SetTime
    tmpargument = STATECHARGE
    SetState
    tmpx = selfx
    tmpy = selfy
    tmpturn = selfturn
    tmpdistance = 200
    Compass
    ClearWaypoints
    AddWaypoint

//------------------------------------------------------------------------------
// ZZ> Handle being attacked by blocking or countering or running away
IfAttacked
  Run
  tmpargument = STATECOMBAT
  SetState
  SetTurnModeToVelocity
  SetTargetToWhoeverAttacked
  tmpargument = rand & 1
  PlaySound

//------------------------------------------------------------------------------
End
//------------------------------------------------------------------------------
